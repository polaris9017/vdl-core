plugins {
    id 'java'
    id "com.github.hierynomus.license" version "0.14.0"
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
}

group 'kr.projectn.vdl'
version '1.0'

sourceCompatibility = 1.8

apply plugin: "jacoco"

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

repositories {
    mavenCentral()
}


license {
    strictCheck true
    ext.year = '2016-' + Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'qscx9512'
    ext.email = 'moonrise917@gmail.com'

    exclude "test/*"
}

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

javadoc {
    options.links 'https://docs.oracle.com/javase/8/docs/api'
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId project.getGroup()
                artifactId project.getName()
                name = 'vdl-core'
                description = 'vdl core API library'
                url = 'https://github.com/polaris9017/vdl-core'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'polaris9017'
                        name = 'polaris9017'
                        email = 'moonrise917@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com/polaris9017/vdl-core.git'
                    developerConnection = 'https://github.com/polaris9017/vdl-core.git'
                    url = 'https://github.com/polaris9017/vdl-core'
                }
            }
        }
    }
}

bintray {
    def Properties prop = new Properties()
    def propFile = file('local.properties')
    if (propFile.canRead()) {
        prop.load(new FileInputStream(propFile))
        if(prop != null && prop.containsKey('bintray.user') && prop.containsKey('bintray.api')) {
            user = prop['bintray.user']
            key = prop['bintray.api']
        }
    }
    publications = ['mavenJava']

    pkg {
        repo = 'vdl'
        name = project.getName()
        websiteUrl = 'https://github.com/polaris9017/vdl-core'
        issueTrackerUrl = 'https://github.com/polaris9017/vdl-core/issues'
        vcsUrl = 'https://github.com/polaris9017/vdl-core.git'
        licenses = ['Apache-2.0']
        labels = ['java']
        publicDownloadNumbers = true
        desc = 'vdl core API library'

        //GitHub repo info
        githubRepo = 'polaris9017/vdl-core'
        githubReleaseNotesFile = 'README'

        version {
            name = project.getVersion()
            desc = 'vdl core API library 1.0 release'
            released  = new Date()
            vcsTag = '1.0'
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = prop['bintray.api'] //Optional. The passphrase for GPG signing'
            }
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7' //Apache Commons Lang library
    implementation group: 'com.comcast', name: 'hlsparserj', version: '1.0.0' //HLS parser library
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0' //Logging library
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5' //JSON parser
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6' //Apache HttpClent
    implementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.6' //Apache HttpClient Fluent API
    implementation group: 'commons-io', name: 'commons-io', version: '2.6' //Apache Commons IO
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.6'
    implementation group: 'com.google.guava', name: 'guava', version: '26.0-jre'
}
